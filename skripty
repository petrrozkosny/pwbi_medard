01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________

05 suma srazek ruzyne = 
SUMX(FILTER(data,data[lokalita] = "RUZYNE"),data[srazky])

_________________________________________________________________

06 suma srazek v poslednim datumu mesice = 
SUMX(
FILTER(data,data[datum] = MAX(data[datum])),
data[srazky])

_______________________________________________________________

07 pocet datumu se srazkami = 
-- v danem mesici, roce, kolikrát srazky > 0
COUNTX(FILTER(data,data[srazky] > 0),data[datum])

______________________________________________________________________

08 pocet datumu pro ruzyni = 
-- kolikrát v daném měsíci a roce ruzyne poskytla data
COUNTX(FILTER(data,data[lokalita] = "RUZYNE"),data[datum])

___________________________________________________________________

09 suma srazek predchoziho mesice daneho roku = 

SUMX(
    FILTER(
        ALL(data),
    data[rok] = MIN(data[rok])
    && data[mesic] = min(data[mesic]) - 1),
data[srazky])

________________________________________________________________

10 podil srazek mesice na sume srazek roku = 

-- suma srazek v danem mesici a roce
SUM(data[srazky])
-- deleno
/
-- suma srazek v danem roce 
SUMX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok])),
data[srazky])
____________________________________________________________

12 nejteplejsi mesic = 

-- Pokud se maximalni teplota daneho roku a mesice rovna 
-- maximalni teplote daneho roku, tak "nejteplejsi" jinak "ostatni"
IF(
-- maximalni maximalni_teplota v danem mesici a roce
MAX(data[maximalni_teplota])
=
-- maximalni maximalni_teplota v danem roce napric mesici
MAXX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok])),
data[maximalni_teplota]), "nejteplejsi","ostatni")

______________________________________________________________

13 pocet datumu maximalni teploty v danem mesici a roce = 
COUNTX(
    FILTER(data,
        data[maximalni_teplota] =  MAX(data[maximalni_teplota])),
data[datum])

____________________________________________________

14 pocet tropickych dnu ruzyne = 
-- pocet datumu, kdy v ruzyni byla maximalni_teplota > 30 a srazky = 0
MAX(
COUNTX(
    FILTER(data,
    data[lokalita] = "RUZYNE"
    && data[maximalni_teplota] > 30
    && data[srazky] = 0
    && NOT ISBLANK(data[srazky])),
data[datum]),0)
____________________________________________________

15 pocet mrazivych dnu ruzyne = 
-- pocet datumu, kdy minimalni_teplota <0 v ruzyni
COUNTX(
    FILTER(data,data[minimalni_teplota] < 0
    && data[lokalita] = "RUZYNE"),
data[datum])

_______________________________________________________________

16 suma srazek predchozi rok = 
-- suma srazek ve stejnem mesici predchoziho roku
SUMX(
FILTER(
    ALL(data),
data[rok] = MIN(data[rok]) - 1
&& data[mesic] = min(data[mesic])),
data[srazky])

________________________________________________

17 datum poprve prselo = 
-- minimalni datum v danem mesici, roce, kdy srazky > 0
MINX(
    FILTER(data,data[srazky] > 0),
data[datum])

_______________________________________________________

18 datum maximalnich srazek ruzyne = 
CONCATENATEX(
    FILTER(data,data[lokalita] = "RUZYNE"
    && data[srazky] = MAXX(FILTER(data,data[lokalita]="RUZYNE"),data[srazky])
    ),
data[datum],", ")

__________________________________

19 pf maximalni teplota = 
-- v danem roce, lokalite
VAR maximalni_teplota = MAX(data[maximalni_teplota])
-- maximalni teplota napric roky, v dane lokalite
VAR maximalni_teplota_vsechny_roky = 

MAXX(
    FILTER(
        ALL(data)
        ,data[lokalita] = SELECTEDVALUE(data[lokalita])),
data[maximalni_teplota])

RETURN
IF(maximalni_teplota = maximalni_teplota_vsechny_roky,"TOMATO")

______________________________

20 cf nazev grafu = 

SWITCH(TRUE(),
HASONEFILTER(data[lokalita]), "Maximalni teploty v lokalite "&SELECTEDVALUE(data[lokalita]),
ISFILTERED(data[lokalita]),"Maximalni teploty v lokalitach" & CONCATENATEX(DISTINCT(data[lokalita]),data[lokalita]," ,"),
"Maximalni teploty za vsechny lokality")

____________________________________

21 prumerne srazky v roce a lokalite = 

-- napric mesici


AVERAGEX(FILTER(ALL(data),data[lokalita] = SELECTEDVALUE(data[lokalita])),data[srazky])

_____________________________

22 prumerne srazky napric mesici = 

-- v dane lokalite, roce

AVERAGEX(FILTER(ALL(data),data[lokalita] = SELECTEDVALUE(data[lokalita]) && data[rok] = SELECTEDVALUE(data[rok])),data[srazky])

_____________________________________

24 pocet vyskytu = 
COUNTX(
FILTER(
    ADDCOLUMNS(
    ALL(
        data[lokalita],data[rok]),
   "rocni srazky",CALCULATE(SUM(data[srazky]))),
[rocni srazky] > SELECTEDVALUE(parametr_srazky[parametr_srazky])),
data[lokalita])
